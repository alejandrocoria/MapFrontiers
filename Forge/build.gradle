plugins {
    id 'multiloader-loader'
    id 'net.minecraftforge.gradle' version '[6.0.24,6.2)'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
}

archivesBaseName = "${project.mod_name}-${project.minecraft_version}"
jar.archiveClassifier = 'forge'

jarJar.enable()
tasks.jarJar.configure {
    archiveClassifier.set('forge')
}

tasks.named("build").configure {
    dependsOn("jarJar")
}

java {
    sourcesJar.enabled = false
}

mixin {
    config("${mod_id}.forge.mixins.json")
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    copyIdeResources = true //Calls processResources when in dev
    reobf = false

    runs {
        configureEach {
            mods {
                modClientRun {
                    source project.sourceSets.main
                }
            }
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
        }
        client {
            ideaModule "${rootProject.name}.${project.name}.main"
            workingDirectory project.file('runs/client')
        }
        server {
            ideaModule "${rootProject.name}.${project.name}.main"
            workingDirectory project.file('runs/server')
        }
        data {
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5-SNAPSHOT:processor'
    implementation('net.sf.jopt-simple:jopt-simple:5.0.4') { version { strictly '5.0.4' } } // https://forums.minecraftforge.net/topic/149009-error-on-launch/

    implementation group: 'mysticdrew', name: 'common-networking-forge', version: project.common_networking_version
    jarJar (group: 'mysticdrew', name: 'common-networking-forge', version: project.common_networking_jarjar_version) {
        jarJar.pin(it, project.common_networking_version)
    }
    implementation "curse.maven:journeymap-${project.jm_project_id}:${project.jm_forge_file_id}"
}

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

jar {
    manifest {
        attributes([
                "MixinConfigs" : "${mod_id}.forge.mixins.json"
        ])
    }
}
