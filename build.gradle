plugins {
	id 'fabric-loom' version '0.12-SNAPSHOT'
	id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = "${archives_base_name}-${minecraft_version}"
version = "${mod_version}-fabric"

repositories {
	maven {
		name = "JourneyMap (Public)"
		url = "https://jm.gserv.me/repository/maven-public/"
	}

	maven {
		url "https://www.cursemaven.com"
	}

	maven {
		url "https://maven.terraformersmc.com/releases"
	}

	maven {
		name = "Fuzs Mod Resources"
		url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi group: 'info.journeymap', name: 'journeymap-api', version: project.journeymap_api_fabric_version, changing: true
	modImplementation "curse.maven:journeymap-${project.jm_project_id}:${project.jm_fabric_file_id}"

	modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:${forge_config_api_port_version}"

	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
}

loom {
	runs {
		client {
			client()
			setConfigName("Fabric Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			serverWithGui()
			setConfigName("Fabric Server")
			ideConfigGenerated(true)
			runDir("run-server")
		}
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}
