plugins {
    id 'java-library'
    id 'maven-publish'
}

base {
    archivesName = "${mod_id}-${project.name}-${minecraft_version}"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
//    withJavadocJar()
}

repositories {
    // used for local mods in the libs folder
    flatDir {
        dirs 'libs'
    }

    mavenLocal()
    mavenCentral()
    // https://docs.gradle.org/current/userguide/declaring_repositories.html#declaring_content_exclusively_found_in_one_repository
    exclusiveContent {
        forRepository {
            maven {
                name = 'Sponge'
                url = 'https://repo.spongepowered.org/repository/maven-public'
            }
        }
        filter { includeGroupAndSubgroups("org.spongepowered") }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'Fabric (needed for Forge Config API Port)'
                url = uri("https://maven.fabricmc.net")
            }
        }
        filter {
            includeGroup("net.fabricmc")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'JourneyMap API and CommonNetworking'
                url = 'https://jm.gserv.me/repository/maven-public/'
            }
        }
        filter {
            includeGroupAndSubgroups("info.journeymap")
            includeGroupAndSubgroups("mysticdrew")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = 'JourneyMap Mod'
                url = 'https://www.cursemaven.com'
            }
        }
        filter {
            includeGroupAndSubgroups("curse.maven")
        }
    }
    maven {
        name = 'ModMenu'
        url = 'https://maven.terraformersmc.com/releases'
    }
    maven {
        name = 'Fuzs Mod Resources'
        url = 'https://raw.githubusercontent.com/Fuzss/modresources/main/maven/'
    }
    maven {
        name = 'BlameJared'
        url = 'https://maven.blamejared.com'
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:24.1.0'
}

// Declare capabilities on the outgoing configurations.
// Read more about capabilities here: https://docs.gradle.org/current/userguide/component_capabilities.html#sec:declaring-additional-capabilities-for-a-local-component
['apiElements', 'runtimeElements', 'sourcesElements'/*, 'javadocElements'*/].each { variant ->
    configurations."$variant".outgoing {
        capability("$group:$mod_id-${project.name}-${minecraft_version}:$version")
        capability("$group:$mod_id:$version")
    }
    publishing.publications.configureEach {
        suppressPomMetadataWarningsFor(variant)
    }
}

sourcesJar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }
}

jar {
    from(rootProject.file("LICENSE")) {
        rename { "${it}_${mod_name}" }
    }

    manifest {
        attributes([
                'Specification-Title'   : mod_name,
                'Specification-Vendor'  : mod_author,
                'Specification-Version' : project.jar.archiveVersion,
                'Implementation-Title'  : project.name,
                'Implementation-Version': project.jar.archiveVersion,
                'Implementation-Vendor' : mod_author,
                'Built-On-Minecraft'    : minecraft_version
        ])
    }
}

processResources {
    def expandProps = [
            "version": version,
            "group": project.group, //Else we target the task's group.
            "java_version": java_version,
            "minecraft_version": minecraft_version,
            "minecraft_version_range": minecraft_version_range,
            "mod_name": mod_name,
            "mod_author": mod_author,
            "mod_id": mod_id,
            "mod_description": mod_description,
            "mod_email": mod_email,
            "mod_homepage": mod_homepage,
            "mod_issues": mod_issues,
            "mod_sources": mod_sources,
            "mod_update_json": mod_update_json,
            "mod_credits": mod_credits,
            "mod_license": mod_license,
            "mod_logo": mod_logo,
            "fabric_version": fabric_version,
            "fabric_loader_version": fabric_loader_version,
            "forge_version": forge_version,
            "forge_loader_version_range": forge_loader_version_range,
            "neoforge_version": neoforge_version,
            "neoforge_loader_version_range": neoforge_loader_version_range,
            "journeymap_min_version": journeymap_min_version,
            "journeymap_version_range": journeymap_version_range
    ]

    filesMatching(['pack.mcmeta', 'fabric.mod.json', 'META-INF/mods.toml', 'META-INF/neoforge.mods.toml', '*.mixins.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId base.archivesName.get()
            from components.java
        }
    }
    repositories {
        maven {
            url System.getenv("local_maven_url")
        }
    }
}
